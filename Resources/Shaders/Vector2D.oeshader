#shadertype VERTEX
cbuffer PVMatrix
{
	matrix pv_matrix;
};

struct Input {
	float2 position : POSITION;
	float2 uv : TEXCOORD;
};

struct Output {
	float4 position : SV_POSITION;
	float2 uv : TEXCOORD;
};

Output main(Input input) {
	Output output;

    output.position = mul(pv_matrix, float4(input.position.x, input.position.y, 0, 1));
	output.uv = input.uv;

	return output;
}

#shadertype FRAGMENT
#include TextureArray.oeshader

struct Input {
	float4 position : SV_POSITION;
	float2 uv : TEXCOORD;
};

struct PaintStyle {
	matrix paintMat;
	float4 innerColor;
	float4 outerColor;
	float2 extent;
	float radius;
	float feather;
	float texid;
	int pad1, pad2, pad3;
};

cbuffer Data
{
	float4 scissor;
	PaintStyle style;
	float texid;
	float strokeMul;
	float pad4, pad5;
};

float calcScissor(float2 p) {
	float2 pos = scissor.xy;
	float2 size = scissor.zw;
	float2 halfSize = size * 0.5f;

	float2 sc = abs(p - pos - halfSize) - halfSize + 0.5;
	return 1.0f - length(max(sc, 0.0));
}

float strokeMask(float2 uv, float strokeMul) {
	return min(1.0, (1.0 - abs(uv.x*2.0 - 1.0)) * strokeMul) * min(1.0, uv.y);
}

float sdroundrect(float2 pt, float2 ext, float rad) {
	float2 ext2 = ext - float2(rad, rad);
	float2 d = abs(pt) - ext2;
	return min(max(d.x, d.y), 0.0) + length(max(d, 0.0)) - rad;
}

float aa(float d) {
    return smoothstep(0.9 - fwidth(d), 0.9, d);
}

float4 main(Input input) : SV_TARGET{
	float2 transformedPos = mul(style.paintMat, float4(input.position.xy, 0, 1));
	float scissor = calcScissor(input.position);

	float d = clamp((sdroundrect(transformedPos, style.extent, style.radius) + style.feather*0.5) / style.feather, 0.0, 1.0);
	float4 color = lerp(style.innerColor, style.outerColor, d);

	if (style.texid > 0) {
		float2 pt = transformedPos / style.extent;
		pt.y = 1.0f - pt.y;
		color *= resolveTexture2DArr(style.texid, pt);
	}

	if (texid > 0)
		color *= resolveTexture2DArr(texid, input.uv);
	else
		color *= aa(strokeMask(input.uv, strokeMul));

	color *= scissor;
	return color;
}